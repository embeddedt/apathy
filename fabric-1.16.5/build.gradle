plugins {
  id 'dev.architectury.loom'
}

evaluationDependsOn(":core") //Gradle Do Not Be Weird Challenge (Impossible)
evaluationDependsOn(":common-1.16.5")

var WHAT = project(":common-1.16.5")

apply plugin: "java"

archivesBaseName = "${project.modId}-fabric-1.16.5"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
	sourceCompatibility = "8"
	targetCompatibility = "8"
}
java.withSourcesJar()

/// deps ///

dependencies {
	//basics
	minecraft "com.mojang:minecraft:1.16.5"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:0.11.3"
	compileOnly "org.jetbrains:annotations:24.0.1"
	
	//fabric api, TODO trim down to required modules
	modImplementation "net.fabricmc.fabric-api:fabric-api:0.34.2+1.16"
	
	//xplat
	implementation WHAT
	implementation project(":core")
}

//fold in xplat resources and sources
processResources {
	from WHAT.sourceSets.main.resources
	from project(":core").sourceSets.main.resources
	
	inputs.property "version", project.version
	filesMatching("**/fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile) {
	source(WHAT.sourceSets.main.allSource)
	source(project(":core").sourceSets.main.allSource)
}

//loom doesn't generate run configs by default in subprojects
loom {
	runs {
		client {
			client()
			setConfigName("Fabric 1.16.5 Client")
			ideConfigGenerated(true)
			runDir("run")
		}
		server {
			server()
			setConfigName("Fabric 1.16.5 Server")
			ideConfigGenerated(true)
			runDir("run")
		}
	}
	
	//nor can it figure out the refmap name in this situation
	mixin {
		defaultRefmapName = "${project.modId}.refmap.json"
		
		//see https://github.com/FabricMC/fabric-loom/issues/456
		//todo, maybe also fixable by updating floader?
		useLegacyMixinAp = true
	}
}

tasks.withType(GenerateModuleMetadata) {
	enabled = false
}
